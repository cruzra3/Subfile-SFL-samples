      *=============================================================*
      *  SFL001DF - CUSTOMER MASTER INQUIRY DISPLAY FILE            *
      *  (c) Copyright Jonathan E. Yergin, 1991 USA                 *
      *    & Wayne Madden, 1993 USA                                 *
      *=============================================================*
      *
     A                                      DSPSIZ(24 80 *DS3) PRINT
     A                                      REF(CUSTPF)
      *
     A          R SFLHDR
     A                                  1  2'SFL001'
     A                                  1 29'Customer Master Inquiry'
     A                                      DSPATR(HI)
      *
     A          R SFL                       SFL
     A            CNAME     R        O  7  5
     A            CCUST     R        O  7 32EDTCDE(Z)
     A            CCITY     R        O  7 40
     A            CSTACD    R        O  7 62
     A            CZIPCD    R        O  7 68
      *
     A          R SFLCTL                    SFLCTL(SFL)
     A                                      BLINK OVERLAY
     A                                      CA03 CA12 PAGEDOWN
     A  40                                  SFLCLR
     A                                      SFLDSPCTL
     A  42                                  SFLDSP
     A  99                                  SFLEND(*MORE)
     A                                      SFLSIZ(16) SFLPAG(15)
     A  60                                  SFLMSG('Roll up or down beyond fir-
     A                                      st or last record in subfile.' 60)
     A            SFLPS          4S 0H      SFLRCDNBR
     A                                  4 30'Position to customer . .'
     A            SEARCH        25A  I  4 55CHECK(LC)
     A                                  6  5'Customer Name              Numb-
     A                                      er  City                 State  Zip'
     A                                      DSPATR(HI)
      *
     A          R SFLCMD
     A                                      OVERLAY
     A                                 23  2'F3=Exit' COLOR(BLU)
     A                                 23 14'F12=Cancel' COLOR(BLU)
