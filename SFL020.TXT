      *==============================================================*
      * SFL020 - Customer Master Search (alternate view)             *
      *          (c) Copyright Jonathan E. Yergin, 1991 USA          *
      *              &  Wayne Madden, 1993 USA                       *
      *==============================================================*
      *
     FCUSTLF  IF  E           K        DISK
     FSFL020DFCF  E                    WORKSTN      KINFDS WSDS
     F                                        SFLRN KSFILE SFL
      *
      * Literals for screen mode
     E                    LIT     1   2  8               Display literals
      *
      * Standard include for all interactive programs
      *   named hexadecimal constants for function keys
     I              X'31'                 C         @F01
     I              X'32'                 C         @F02
     I              X'33'                 C         @F03
     I              X'34'                 C         @F04
     I              X'35'                 C         @F05
     I              X'36'                 C         @F06
     I              X'37'                 C         @F07
     I              X'38'                 C         @F08
     I              X'39'                 C         @F09
     I              X'3A'                 C         @F10
     I              X'3B'                 C         @F11
     I              X'3C'                 C         @F12
     I              X'B1'                 C         @F13
     I              X'B2'                 C         @F14
     I              X'B3'                 C         @F15
     I              X'B4'                 C         @F16
     I              X'B5'                 C         @F17
     I              X'B6'                 C         @F18
     I              X'B7'                 C         @F19
     I              X'B8'                 C         @F20
     I              X'B9'                 C         @F21
     I              X'BA'                 C         @F22
     I              X'BB'                 C         @F23
     I              X'BC'                 C         @F24
     I              X'BD'                 C         @CLEAR
     I              X'F1'                 C         @ENTER
     I              X'F3'                 C         @HELP
     I              X'F4'                 C         @PAGUP
     I              X'F5'                 C         @PAGDN
     I              X'F6'                 C         @PRINT
      *
      * Standard include for all interactive programs
      *   Defines standard constants
     I              '0'                   C         @FALSE
     I              '1'                   C         @TRUE
      *
      * externally described data structure for records
     ILINEDS    E DSCUSTPF                   16
      *
      * Standard include for all interactive programs
      *   display file information data structure
     IWSDS        DS
      *               Identifies the key pressed
     I                                      369 369 KEY
      *               Identifies the subfile page rrn
     I                                    B 378 3790PAGRRN
      *
      *==============================================================*
      *  Program entry parameter list.                               *
      *==============================================================*
      *
     C           *ENTRY    PLIST
     C                     PARM           I#MODE
     C                     PARM O#CUST    I#CUST
     C                     PARM O#KEY     I#KEY
      *
     C                     MOVE I#MODE    SCNMOD
      *
      *==============================================================*
      * Reset variables and clear subfile                            *
      *   in case program called again after *INLR = *OFF            *
      *   ensures that program starts again properly                 *
      *==============================================================*
      *
     C                     RESETENDNAM
     C                     RESETENDCUS
     C                     RESET@EXIT
      *
      *==============================================================*
      * Program mainline                                             *
      *==============================================================*
      *
     C                     EXSR @LOAD
      *
      * Perform until exit requested using F03 or F12
     C           @EXIT     DOUEQ@TRUE
      *
     C                     WRITESFLHDR
     C                     WRITESFLCMD
     C                     EXFMTSFLCTL
      *
      * Process response
     C           KEY       CASEQ@F03      @F03SR            F03 exit
     C           KEY       CASEQ@F11      @F11SR            F11 View
     C           KEY       CASEQ@F12      @F12SR            F12 cancel
     C           KEY       CASEQ@PAGUP    @PGUP             Page up
     C           KEY       CASEQ@PAGDN    @PGDN             Page down
     C                     CAS            @ENTKY            Enter
     C                     ENDCS
      *
     C                     ENDDO
      *
      * End of program, you can modify to leave *INLR = *OFF
      *   and program will still work properly when called again
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *==============================================================*
      * Subroutine Section of Program                                *
      *==============================================================*
      *
      *==============================================================*
      * Initialization Subroutine                                    *
      *==============================================================*
      *
     C           *INZSR    BEGSR
      *
     C                     MOVE *BLANK    SCNMOD  1
     C                     Z-ADD0         SFLRN   40
     C           *LIKE     DEFN SCNMOD    I#MODE
     C           *LIKE     DEFN SFLRN     OCCUR
     C           *LIKE     DEFN SFLRN     BOTLIN
     C           *LIKE     DEFN SFLRN     PRIOR
     C           *LIKE     DEFN CNAME     ENDNAM
     C           *LIKE     DEFN CCUST     ENDCUS
     C           *LIKE     DEFN CCUST     I#CUST
     C           *LIKE     DEFN CCUST     O#CUST
     C           *LIKE     DEFN KEY       I#KEY
     C           *LIKE     DEFN KEY       O#KEY
      *
      * Create program exit flag and set value to @FALSE
     C                     MOVE @FALSE    @EXIT   1
      *
      * Miscellaneous subfile program variables
      * Set up view #1
     C                     MOVE *ON       *IN11
     C                     MOVE *OFF      *IN12
     C                     MOVE *OFF      *IN13
     C                     CLEARENDNAM
     C                     CLEARENDCUS
      *
      * Complete file key
     C           FILKEY    KLIST
     C                     KFLD           CNAME
     C                     KFLD           CCUST
      *
      * End key for repositioning subfile
     C           ENDKEY    KLIST
     C                     KFLD           ENDNAM
     C                     KFLD           ENDCUS
      *
     C                     ENDSR
      *
      *==============================================================*
      * Load subfile records into MODS                               *
      *==============================================================*
      *
     C           @LOAD     BEGSR
      *
      *  Set up display mode.
     C           SCNMOD    IFEQ 'I'
     C                     MOVE LIT,1     DSPMOD
     C                     MOVE *OFF      *IN05
     C                     ELSE
     C                     MOVE LIT,2     DSPMOD
     C                     MOVE *ON       *IN05
     C                     ENDIF
      *
      *  Reset bottom line number.
     C                     Z-ADD0         BOTLIN
      *
      *  Position file after last record read; set BOF indicator.
     C           ENDKEY    SETGTCUSTLF
     C                     READPCUSTLF                   92
      *
      *  Continue with record after last record read.
     C           ENDKEY    SETGTCUSTLF
      *
      *    Load the multiple occurence data structure.
     C                     DO   15        OCCUR
     C           OCCUR     OCUR LINEDS
      *    Read the vendor master file.
     C                     READ CUSTLF                   91
     C           *IN91     IFEQ *ON
     C                     LEAVE
     C                     ELSE
      *  Save end key fields and bottom line.
     C                     EXSR @ENDKY
     C                     Z-ADDOCCUR     BOTLIN
     C                     ENDIF
     C                     ENDDO
      *
      *  If not EOF, read one more record to make sure.
     C           *IN91     IFEQ *OFF
     C           16        OCUR LINEDS
     C                     READ CUSTLF                   91
     C                     ENDIF
      *
      *  If no records added to data structure...
     C           BOTLIN    IFEQ 0
      *  Set SFLDSP off, set error on if SEARCH field changed.
     C                     MOVE *OFF      *IN42
     C                     ELSE
      *  Set SFLDSP on, error off.
     C                     MOVE *ON       *IN42
      *  Build subfile
     C                     EXSR @BLDSF
     C                     END
      *
     C                     ENDSR
      *
      *==============================================================*
      *  Build subfile                                               *
      *==============================================================*
      *
     C           @BLDSF    BEGSR
      *
      *  Clear subfile.
     C                     EXSR @RESET
      *
      *  Load subfile from multi-occurence data structure.
     C                     CLEARSELIO
     C                     DO   BOTLIN    SFLRN
     C           SFLRN     OCUR LINEDS
     C                     WRITESFL
     C                     ENDDO
      *
     C                     ENDSR
      *
      *==============================================================*
      * Update end keys and variables for file/subfile               *
      *==============================================================*
      *
     C           @ENDKY    BEGSR
      *
     C                     MOVE CNAME     ENDNAM
     C                     Z-ADDCCUST     ENDCUS
      *
     C                     ENDSR
      *
      *==============================================================*
      * Clear subfile (one or two subfiles)                          *
      *==============================================================*
      *
     C           @RESET    BEGSR
      *
      * Clear subfile and reset subfile display indicator.
     C                     MOVE *ON       *IN40
     C                     WRITESFLCTL
     C                     MOVE *OFF      *IN40
      *
      * Reset subfile record number and subfile position fields.
     C                     RESETSFLRN
      *
     C                     ENDSR
      *
      *==============================================================*
      * Reposition file                                              *
      *==============================================================*
      *
     C           @REPOS    BEGSR
      *
      * Reposition file by search value
     C           ENDKEY    SETLLCUSTLF
     C                     EXSR @LOAD
      *
     C                     ENDSR
      *
      *==============================================================*
      * Subfile page up                                              *
      *==============================================================*
      *
     C           @PGUP     BEGSR
      *
      * More records in file exist
     C           *IN92     IFEQ *OFF
      *
      * Continue with record after the last one read.
     C           ENDKEY    SETLLCUSTLF
     C           BOTLIN    ADD  15        PRIOR
     C                     DO   PRIOR
     C                     READPCUSTLF                   92
     C           *IN92     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
      *
      * Set up end key fields
     C           *IN92     IFEQ *ON
     C                     CLEARENDNAM
     C                     CLEARENDCUS
     C                     ELSE
     C                     EXSR @ENDKY
     C                     ENDIF
      *
     C                     EXSR @LOAD
      *
      * ELSE, no more records in file
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Subfile page down                                            *
      *==============================================================*
      *
     C           @PGDN     BEGSR
      *
     C           *IN91     IFEQ *OFF
      * Continue with record after the last one read.
     C           ENDKEY    SETGTCUSTLF
     C                     EXSR @LOAD
      * Page down beyond end of subfile
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Process enter key after subfile display                      *
      *==============================================================*
      *
     C           @ENTKY    BEGSR
      *
      * User request position to new name
     C           SEARCH    IFNE *BLANKS
     C                     MOVELSEARCH    ENDNAM    P
     C                     CLEARENDCUS
     C                     EXSR @REPOS
      * or process subfile changed records
     C                     ELSE
     C                     EXSR @SFLRD
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Read changed records in subfile to check for selection       *
      *==============================================================*
      *
     C           @SFLRD    BEGSR
      *
      * Did user make a selection?
     C                     READCSFL                      98
      *
     C           *IN98     DOWEQ*OFF
      *
      * If user made selection,
      *   initialize output variable with user selection
      *   and force end of program.
     C           SELIO     IFEQ 1
     C                     Z-ADDCCUST     O#CUST
     C                     MOVE @TRUE     @EXIT
     C                     LEAVE
     C                     ENDIF
      *
     C                     READCSFL                      98
     C                     ENDDO
      *
     C                     ENDSR
      *
      *==============================================================*
      * F03 key subroutine                                           *
      *==============================================================*
      *
     C           @F03SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
     C                     MOVE KEY       O#KEY
      *
     C                     ENDSR
      *
      *==============================================================*
      * F11 key subroutine                                           *
      *==============================================================*
      *
     C           @F11SR    BEGSR
      *
      *
      *  If View 1, change to View 2.
     C           *IN11     IFEQ *ON
     C                     MOVE *OFF      *IN11
     C                     MOVE *ON       *IN12
     C                     MOVE *OFF      *IN13
     C                     ELSE
      *    If View 2, change to View 3.
     C           *IN12     IFEQ *ON
     C                     MOVE *OFF      *IN11
     C                     MOVE *OFF      *IN12
     C                     MOVE *ON       *IN13
     C                     ELSE
      *    Else, change back to View 1.
     C                     MOVE *ON       *IN11
     C                     MOVE *OFF      *IN12
     C                     MOVE *OFF      *IN13
     C                     ENDIF
     C                     ENDIF
      *
      *  Load subfile.
     C                     EXSR @BLDSF
      *
     C                     ENDSR
      *
      *==============================================================*
      * F12 key subroutine                                           *
      *==============================================================*
      *
     C           @F12SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
     C                     MOVE KEY       O#KEY
      *
     C                     ENDSR
      *
**  LIT - Display literals
Inquiry
Search
