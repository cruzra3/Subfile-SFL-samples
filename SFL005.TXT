      *==============================================================*
      * SFL005 - Item Transaction Review w/ READC                    *
      *          (c) Copyright Jonathan E. Yergin, 1991 USA          *
      *              &  Wayne Madden, 1993 USA                       *
      *==============================================================*
      *
     FTRANSPF UF  E           K        DISK
     FITEMPF  IF  E           K        DISK
     FSFL005DFCF  E                    WORKSTN      KINFDS WSDS
     F                                        SFLRN KSFILE SFL
      *
      * Standard include for all interactive programs
      *   named hexadecimal constants for function keys
     I              X'31'                 C         @F01
     I              X'32'                 C         @F02
     I              X'33'                 C         @F03
     I              X'34'                 C         @F04
     I              X'35'                 C         @F05
     I              X'36'                 C         @F06
     I              X'37'                 C         @F07
     I              X'38'                 C         @F08
     I              X'39'                 C         @F09
     I              X'3A'                 C         @F10
     I              X'3B'                 C         @F11
     I              X'3C'                 C         @F12
     I              X'B1'                 C         @F13
     I              X'B2'                 C         @F14
     I              X'B3'                 C         @F15
     I              X'B4'                 C         @F16
     I              X'B5'                 C         @F17
     I              X'B6'                 C         @F18
     I              X'B7'                 C         @F19
     I              X'B8'                 C         @F20
     I              X'B9'                 C         @F21
     I              X'BA'                 C         @F22
     I              X'BB'                 C         @F23
     I              X'BC'                 C         @F24
     I              X'BD'                 C         @CLEAR
     I              X'F1'                 C         @ENTER
     I              X'F3'                 C         @HELP
     I              X'F4'                 C         @PAGUP
     I              X'F5'                 C         @PAGDN
     I              X'F6'                 C         @PRINT
      *
      * Standard include for all interactive programs
      *   Defines standard constants
     I              '0'                   C         @FALSE
     I              '1'                   C         @TRUE
      *
      * Standard record deleted message for subfile
     I              '* Record deleted *'  C         #DMSG
      *
      * Standard include for all interactive programs
      *   display file information data structure
     IWSDS        DS
      *               Identifies the key pressed
     I                                      369 369 KEY
      *               Identifies the subfile page rrn
     I                                    B 378 3790PAGRRN
      *
      *==============================================================*
      * Reset variables and clear subfile                            *
      *   in case program called again after *INLR = *OFF            *
      *   ensures that program starts again properly                 *
      *==============================================================*
      *
     C                     RESET@EXIT
     C                     EXSR @RESET
      *
      *==============================================================*
      * Program mainline                                             *
      *==============================================================*
      *
     C                     EXSR @LOAD
      *
      * Perform until exit requested using F03 or F12
     C           @EXIT     DOUEQ@TRUE
      *
     C                     WRITESFLHDR
     C                     WRITESFLCMD
     C                     EXFMTSFLCTL
      *
     C                     Z-ADDPAGRRN    SFLPS
      *
      * Process response
     C           KEY       CASEQ@F03      @F03SR            F03 exit
     C           KEY       CASEQ@F12      @F12SR            F12 cancel
     C                     CAS            @ENTKY            Enter
     C                     ENDCS
      *
     C                     ENDDO
      *
      * End of program, you can modify to leave *INLR = *OFF
      *   and program will still work properly when called again
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *==============================================================*
      * Subroutine Section of Program                                *
      *==============================================================*
      *
      *==============================================================*
      * Initialization Subroutine                                    *
      *==============================================================*
      *
     C           *INZSR    BEGSR
      *
     C           *LIKE     DEFN DITEM     KITEM
     C           *LIKE     DEFN DVEND     KVEND
      *
      * Create program exit flag and set value to @FALSE
     C                     MOVE @FALSE    @EXIT   1
      * Create error flag and set value to @FALSE
     C                     MOVE @FALSE    @ERR    1
      *
      * Miscellaneous subfile program variables
     C                     Z-ADD0         SFLRN   40
     C                     Z-ADD0         ENDRN   40
     C                     Z-ADD1         SFLPS
      *
      * Detail file key
     C           DTLKEY    KLIST
     C                     KFLD           KITEM
     C                     KFLD           KVEND
      * Item key
     C           ITMKEY    KLIST
     C                     KFLD           KVEND
     C                     KFLD           KITEM
      *
     C                     ENDSR
      *
      *==============================================================*
      * Load subfile                                                 *
      *==============================================================*
      *
     C           @LOAD     BEGSR
      *
      * Read ALL records into the subfile.
     C           *LOVAL    SETLLTRANSPF
     C                     READ TRANSPF                  99
      *
     C           *IN99     DOWEQ*OFF
      *
      *    Retrieve item description.
     C                     Z-ADDDVEND     KVEND
     C                     Z-ADDDITEM     KITEM
     C           ITMKEY    CHAINITEMPF               21
     C           *IN21     IFEQ *ON
     C                     MOVE *ALL'*'   IDESC
     C                     ENDIF
      *    Move fields.
     C                     Z-ADDDITEM     SITEM
     C                     Z-ADDDVEND     SVEND
     C                     Z-ADDDQNTY     SQNTY
      *    Write subfile record
     C                     ADD  1         SFLRN
     C                     Z-ADDSFLRN     SRRN
     C                     WRITESFL
      *
     C                     READ TRANSPF                  99
     C                     ENDDO
      *
      * If no records were added to subfile, set error indicators.
     C           SFLRN     IFEQ *ZEROS
     C                     MOVE *OFF      *IN42
     C                     ELSE
      * Else, set indicators to display subfile.
     C                     MOVE *ON       *IN42
     C                     Z-ADDSFLRN     ENDRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Clear/Reset Subfile                                          *
      *==============================================================*
      *
     C           @RESET    BEGSR
      *
      * Clear subfile and reset subfile display indicator.
     C                     MOVE *ON       *IN40
     C                     WRITESFLCTL
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN42
      *
      * Reset subfile record number and subfile position fields.
     C                     RESETSFLRN
     C                     RESETSFLPS
      *
     C                     ENDSR
      *
      *==============================================================*
      * Process enter key after subfile display                      *
      *==============================================================*
      *
     C           @ENTKY    BEGSR
      *
      * User request position to new name
     C           NEWPS     IFNE 0
     C                     Z-ADDNEWPS     SFLPS
      * Make sure a valid NEWPS was entered
     C           SFLPS     IFGT ENDRN
     C                     Z-ADDENDRN     SFLPS
     C                     ELSE
     C           SFLPS     IFLT 1
     C                     Z-ADD1         SFLPS
     C                     ENDIF
     C                     ENDIF
      * or process subfile changed records
     C                     ELSE
     C                     EXSR @SFLRD
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Read changed records in subfile to check for selection       *
      *==============================================================*
      *
     C           @SFLRD    BEGSR
      *
      * Did user make a selection?
     C                     READCSFL                      98
      *
     C           *IN98     DOWEQ*OFF
      *
      * Check for delete request
     C           SELIO     IFEQ 4
     C                     EXSR @DELRC
      * Display deleted message and reverse image that sfl record
     C                     CLEARSFL
     C                     Z-ADDSFLRN     SRRN
     C                     MOVEL#DMSG     IDESC     P
     C                     MOVE *ON       *IN09
     C                     UPDATSFL
     C                     MOVE *OFF      *IN09
     C                     ELSE
      * Update request
     C                     EXSR @TRNED
     C           @ERR      IFEQ @FALSE
     C                     EXSR @UPDRC
     C                     ENDIF
     C                     ENDIF
      *
     C                     READCSFL                      98
     C                     ENDDO
      *
     C                     ENDSR
      *
      *==============================================================*
      *  Edit Transaction record fields.                             *
      *==============================================================*
      *
     C           @TRNED    BEGSR
      *
      *  Reset error flag.
     C                     RESET@ERR
      *
      *  Perform edits on the input field SQNTY, set error flag
     C******     SQNTY     IF.........
     C******               ...........
     C******               MOVE @TRUE     @ERR
     C******               ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      *  Update record in the transaction file.                      *
      *==============================================================*
      *
     C           @UPDRC    BEGSR
      *
     C                     Z-ADDSITEM     KITEM
     C                     Z-ADDSVEND     KVEND
     C           DTLKEY    CHAINTRANSPF              21
     C           *IN21     IFEQ *OFF
     C                     Z-ADDSQNTY     DQNTY
     C                     UPDATTRANS
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      *  Delete record in the transaction file.                      *
      *==============================================================*
      *
     C           @DELRC    BEGSR
      *
     C                     Z-ADDSITEM     KITEM
     C                     Z-ADDSVEND     KVEND
     C           DTLKEY    DELETTRANS                21
      *
     C                     ENDSR
      *
      *==============================================================*
      * F03 key subroutine                                           *
      *==============================================================*
      *
     C           @F03SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
      *
     C                     ENDSR
      *
      *==============================================================*
      * F12 key subroutine                                           *
      *==============================================================*
      *
     C           @F12SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
      *
     C                     ENDSR
      *
