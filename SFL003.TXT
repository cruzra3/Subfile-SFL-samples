      *==============================================================*
      * SFL003 - Customer Master Inquiry                             *
      *          (c) Copyright Jonathan E. Yergin, 1991 USA          *
      *              &  Wayne Madden, 1993 USA                       *
      *                                                              *
      * (Added subfile record selection cabality)                    *
      * (Changes from SFL001 are marked)                             *
      *==============================================================*
      *
     FCUSTLF  IF  E           K        DISK
->   FSFL003DFCF  E                    WORKSTN      KINFDS WSDS
     F                                        SFLRN KSFILE SFL
      *
      * Standard include for all interactive programs
      *   named hexadecimal constants for function keys
     I              X'31'                 C         @F01
     I              X'32'                 C         @F02
     I              X'33'                 C         @F03
     I              X'34'                 C         @F04
     I              X'35'                 C         @F05
     I              X'36'                 C         @F06
     I              X'37'                 C         @F07
     I              X'38'                 C         @F08
     I              X'39'                 C         @F09
     I              X'3A'                 C         @F10
     I              X'3B'                 C         @F11
     I              X'3C'                 C         @F12
     I              X'B1'                 C         @F13
     I              X'B2'                 C         @F14
     I              X'B3'                 C         @F15
     I              X'B4'                 C         @F16
     I              X'B5'                 C         @F17
     I              X'B6'                 C         @F18
     I              X'B7'                 C         @F19
     I              X'B8'                 C         @F20
     I              X'B9'                 C         @F21
     I              X'BA'                 C         @F22
     I              X'BB'                 C         @F23
     I              X'BC'                 C         @F24
     I              X'BD'                 C         @CLEAR
     I              X'F1'                 C         @ENTER
     I              X'F3'                 C         @HELP
     I              X'F4'                 C         @PAGUP
     I              X'F5'                 C         @PAGDN
     I              X'F6'                 C         @PRINT
      *
      * Standard include for all interactive programs
      *   Defines standard constants
     I              '0'                   C         @FALSE
     I              '1'                   C         @TRUE
      *
      * Standard include for all interactive programs
      *   display file information data structure
     IWSDS        DS
      *               Identifies the key pressed
     I                                      369 369 KEY
      *               Identifies the subfile page rrn
     I                                    B 378 3790PAGRRN
      *
->    *==============================================================*
->    * Input/Output Parameter List                                  *
->    *    I#CUST - Input customer number (unused as input)          *
->    *    O#CUST - Output customer number (customer selection)      *
->    *    I#KEY  - Input function key (unused as input)             *
->    *    O#KEY  - Output function key (if any)                     *
->    *==============================================================*
->    *
->   C           *ENTRY    PLIST
->   C                     PARM O#CUST    I#CUST
->   C                     PARM O#KEY     I#KEY
->    *
      *==============================================================*
      * Reset variables and clear subfile                            *
      *   in case program called again after *INLR = *OFF            *
      *   ensures that program starts again properly                 *
      *==============================================================*
      *
     C                     RESET@EXIT
     C                     EXSR @RESET
      *
      *==============================================================*
      * Program mainline                                             *
      *==============================================================*
      *
     C                     EXSR @LOAD
      *
      * Perform until exit requested using F03 or F12
     C           @EXIT     DOUEQ@TRUE
      *
     C                     WRITESFLHDR
     C                     WRITESFLCMD
     C                     EXFMTSFLCTL
      *
     C                     Z-ADDPAGRRN    SFLPS
      *
      * Process response
     C           KEY       CASEQ@F03      @F03SR            F03 exit
     C           KEY       CASEQ@F12      @F12SR            F12 cancel
     C           KEY       CASEQ@PAGDN    @PGDN             Page down
     C                     CAS            @ENTKY            Enter
     C                     ENDCS
      *
     C                     ENDDO
      *
      * End of program, you can modify to leave *INLR = *OFF
      *   and program will still work properly when called again
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *==============================================================*
      * Subroutine Section of Program                                *
      *==============================================================*
      *
      *==============================================================*
      * Initialization Subroutine                                    *
      *==============================================================*
      *
     C           *INZSR    BEGSR
      *
     C           *LIKE     DEFN CNAME     ENDNAM
     C           *LIKE     DEFN CCUST     ENDCUS
->   C           *LIKE     DEFN CCUST     I#CUST
->   C           *LIKE     DEFN CCUST     O#CUST
->   C           *LIKE     DEFN KEY       I#KEY
->   C           *LIKE     DEFN KEY       O#KEY
      *
      * Create program exit flag and set value to @FALSE
     C                     MOVE @FALSE    @EXIT   1
      *
      * Miscellaneous subfile program variables
     C                     Z-ADD0         SFLRN   40
     C                     Z-ADD0         ENDRN   40
     C                     Z-ADD1         SFLPS
      *
      * Complete file key
     C           FILKEY    KLIST
     C                     KFLD           CNAME
     C                     KFLD           CCUST
      *
      * End key for repositioning subfile
     C           ENDKEY    KLIST
     C                     KFLD           ENDNAM
     C                     KFLD           ENDCUS
      *
     C                     ENDSR
      *
      *==============================================================*
      * Load subfile                                                 *
      *==============================================================*
      *
     C           @LOAD     BEGSR
      *
      * Read a page of records or until EOF.
     C                     DO   15
     C                     READ CUSTLF                   99
      *
     C           *IN99     IFEQ *ON
     C                     LEAVE
     C                     ELSE
      * Write subfile record.
     C                     ADD  1         SFLRN
     C                     WRITESFL
     C                     ENDIF
      *
     C                     ENDDO
      *
      * Set endkey values
     C                     EXSR @ENDKY
      *
      * If not EOF, read one more record to make sure.
     C           *IN99     IFEQ *OFF
     C                     READ CUSTLF                   99
     C                     ENDIF
      *
      * If no records were added to subfile, set error indicators.
     C           SFLRN     IFEQ *ZEROS
     C                     MOVE *OFF      *IN42
     C                     ELSE
      * Else, set indicators to display subfile.
     C                     MOVE *ON       *IN42
      * Set subfile position to page just added.
     C                     Z-ADDSFLRN     SFLPS
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Update end keys and variables for file/subfile               *
      *==============================================================*
      *
     C           @ENDKY    BEGSR
      *
     C                     MOVE CNAME     ENDNAM
     C                     MOVE CCUST     ENDCUS
     C                     Z-ADDSFLRN     ENDRN
      *
     C                     ENDSR
      *
      *==============================================================*
      * Clear/Reset Subfile                                          *
      *==============================================================*
      *
     C           @RESET    BEGSR
      *
      * Clear subfile and reset subfile display indicator.
     C                     MOVE *ON       *IN40
     C                     WRITESFLCTL
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN42
      *
      * Reset subfile record number and subfile position fields.
     C                     RESETSFLRN
     C                     RESETENDRN
     C                     RESETSFLPS
      *
     C                     ENDSR
      *
      *==============================================================*
      * Reposition file                                              *
      *==============================================================*
      *
     C           @REPOS    BEGSR
      *
      * Clear subfile
     C                     EXSR @RESET
      *
      * Reposition file by search value
     C           CNAME     SETLLCUSTLF
     C                     EXSR @LOAD
      *
     C                     ENDSR
      *
      *==============================================================*
      * Subfile page down                                            *
      *==============================================================*
      *
     C           @PGDN     BEGSR
      *
     C           *IN99     IFEQ *OFF
      * Continue with record after the last one read.
     C           ENDKEY    SETGTCUSTLF
     C                     Z-ADDENDRN     SFLRN
     C                     EXSR @LOAD
      * Page down beyond end of subfile
     C                     ELSE
     C                     MOVE *ON       *IN60
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Process enter key after subfile display                      *
      *==============================================================*
      *
     C           @ENTKY    BEGSR
      *
      * User request position to new name
     C           SEARCH    IFNE *BLANKS
     C                     MOVELSEARCH    CNAME
     C                     EXSR @REPOS
->    * or process subfile changed records
->   C                     ELSE
->   C                     EXSR @SFLRD
     C                     ENDIF
      *
     C                     ENDSR
      *
->    *==============================================================*
->    * Read changed records in subfile to check for selection       *
->    *==============================================================*
->    *
->   C           @SFLRD    BEGSR
->    *
->    * Did user make a selection?
->   C                     READCSFL                      98
->    *
->   C           *IN98     DOWEQ*OFF
->    *
->    * If user made selection,
->    *   initialize output variable with user selection
->    *   and force end of program.
->   C           SELIO     IFEQ 1
->   C                     Z-ADDCCUST     O#CUST
->   C                     MOVE @TRUE     @EXIT
->   C                     LEAVE
->   C                     ENDIF
->    *
->   C                     READCSFL                      98
->   C                     ENDDO
->    *
->   C                     ENDSR
->    *
      *==============================================================*
      * F03 key subroutine                                           *
      *==============================================================*
      *
     C           @F03SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
->   C                     MOVE KEY       O#KEY
      *
     C                     ENDSR
      *
      *==============================================================*
      * F12 key subroutine                                           *
      *==============================================================*
      *
     C           @F12SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
->   C                     MOVE KEY       O#KEY
      *
     C                     ENDSR
      *
