      *==============================================================*
      * SFL014 - Invoice/Payment Matching                            *
      *          (c) Copyright Jonathan E. Yergin, 1991 USA          *
      *              &  Wayne Madden, 1993 USA                       *
      *==============================================================*
      *
     FINVPF   IF  E           K        DISK
     FPAYPF   IF  E           K        DISK
     FSFL014DFCF  E                    WORKSTN      KINFDS WSDS
     F                                        SFLRN1KSFILE SFL1
     F                                        SFLRN2KSFILE SFL2
      *
      * Standard include for all interactive programs
      *   named hexadecimal constants for function keys
     I              X'31'                 C         @F01
     I              X'32'                 C         @F02
     I              X'33'                 C         @F03
     I              X'34'                 C         @F04
     I              X'35'                 C         @F05
     I              X'36'                 C         @F06
     I              X'37'                 C         @F07
     I              X'38'                 C         @F08
     I              X'39'                 C         @F09
     I              X'3A'                 C         @F10
     I              X'3B'                 C         @F11
     I              X'3C'                 C         @F12
     I              X'B1'                 C         @F13
     I              X'B2'                 C         @F14
     I              X'B3'                 C         @F15
     I              X'B4'                 C         @F16
     I              X'B5'                 C         @F17
     I              X'B6'                 C         @F18
     I              X'B7'                 C         @F19
     I              X'B8'                 C         @F20
     I              X'B9'                 C         @F21
     I              X'BA'                 C         @F22
     I              X'BB'                 C         @F23
     I              X'BC'                 C         @F24
     I              X'BD'                 C         @CLEAR
     I              X'F1'                 C         @ENTER
     I              X'F3'                 C         @HELP
     I              X'F4'                 C         @PAGUP
     I              X'F5'                 C         @PAGDN
     I              X'F6'                 C         @PRINT
      *
      * Standard include for all interactive programs
      *   Defines standard constants
     I              '0'                   C         @FALSE
     I              '1'                   C         @TRUE
      *
      * Standard include for all interactive programs
      *   display file information data structure
     IWSDS        DS
      *               Identifies the key pressed
     I                                      369 369 KEY
      *               Identifies the subfile page rrn
     I                                    B 378 3790PAGRRN
      *
      *==============================================================*
      * Reset variables and clear subfile                            *
      *   in case program called again after *INLR = *OFF            *
      *   ensures that program starts again properly                 *
      *==============================================================*
      *
     C                     EXSR @RESET
      *
      *==============================================================*
      * Program mainline                                             *
      *==============================================================*
      *
     C                     EXSR @LOAD
      *
      * Perform until exit requested using F03 or F12
     C           @EXIT     DOUEQ@TRUE
      *
     C                     WRITESFLHDR
     C                     WRITESFLCMD
     C                     WRITESFLCTL1
     C                     WRITESFLCTL2
     C                     READ SFLCTL1                  95
     C                     READ SFLCTL2                  95
      *
      * Process response
     C           KEY       CASEQ@F03      @F03SR            F03 exit
     C           KEY       CASEQ@F12      @F12SR            F12 cancel
     C                     CAS            @SFLRD            Enter
     C                     ENDCS
      *
     C                     ENDDO
      *
      * End of program, you can modify to leave *INLR = *OFF
      *   and program will still work properly when called again
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *==============================================================*
      * Subroutine Section of Program                                *
      *==============================================================*
      *
      *==============================================================*
      * Initialization Subroutine                                    *
      *==============================================================*
      *
     C           *INZSR    BEGSR
      *
      * Create program exit flag and set value to @FALSE
     C                     MOVE @FALSE    @EXIT   1
      * Create error flag and set value to @FALSE
     C                     MOVE @FALSE    @ERR    1
      *
      * Miscellaneous subfile program variables
     C                     Z-ADD0         SFLRN1  40
     C                     Z-ADD0         SFLRN2  40
      *
     C                     ENDSR
      *
      *==============================================================*
      * Load subfile                                                 *
      *==============================================================*
      *
     C           @LOAD     BEGSR
      *
      * Load subfile with invoice data.
     C           *LOVAL    SETLLINVPF
     C                     READ INVPF                    99
      *
     C           *IN99     DOWEQ*OFF
     C           IIDAT     MULT 100.0001  IIMDY
     C                     ADD  1         SFLRN1
     C                     WRITESFL1
     C                     READ INVPF                    99
     C                     ENDDO
      *
      * Load subfile with payment data.
     C           *LOVAL    SETLLPAYPF
     C                     READ PAYPF                    99
      *
     C           *IN99     DOWEQ*OFF
     C           PCDAT     MULT 100.0001  PCMDY
     C                     ADD  1         SFLRN2
     C                     WRITESFL2
     C                     READ PAYPF                    99
     C                     ENDDO
      *
      * If no records were added to subfile 1, set error indicators.
     C           SFLRN1    IFEQ *ZEROS
     C                     MOVE *OFF      *IN42
     C                     ELSE
      * Else, set indicators to display subfile (starting w/ first page)
     C                     MOVE *ON       *IN42
     C                     Z-ADD1         SFLRN1
     C                     ENDIF
      *
      * If no records were added to subfile 2, set error indicators.
     C           SFLRN2    IFEQ *ZEROS
     C                     MOVE *OFF      *IN43
     C                     ELSE
      * Else, set indicators to display subfile (starting w/ first page)
     C                     MOVE *ON       *IN43
     C                     Z-ADD1         SFLRN2
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * Clear subfiles                                               *
      *==============================================================*
      *
     C           @RESET    BEGSR
      *
      * Clear subfile and reset subfile display indicator.
     C                     MOVE *ON       *IN40
     C                     WRITESFLCTL1
     C                     WRITESFLCTL2
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN42
     C                     MOVE *OFF      *IN43
      *
      * Reset subfile record number and subfile position fields.
     C                     RESETSFLRN1
     C                     RESETSFLRN2
      *
     C                     ENDSR
      *
      *==============================================================*
      * Read changed records in subfile to check for selection       *
      *==============================================================*
      *
     C           @SFLRD    BEGSR
      *
      *  Clear return variables and message display indicator.
     C                     CLEARCUSNBR
     C                     CLEARINVNBR
     C                     CLEARCHKNBR
     C                     MOVE *OFF      *IN11
     C                     RESET@ERR
      *
      *  Read changed subfile records for Invoices.
     C                     READCSFL1                     98
     C           *IN98     DOWEQ*OFF
      * Return the Customer and Invoice numbers of selected record.
     C           SELIO     IFEQ 1
     C                     Z-ADDICUST     CUSNBR
     C                     Z-ADDIINV#     INVNBR
     C                     CLEARSELIO
     C                     UPDATSFL1
     C                     LEAVE
     C                     ENDIF
     C                     READCSFL1                     98
     C                     ENDDO
      *
      *  Read changed subfile records for Payments.
     C                     READCSFL2                     98
     C           *IN98     DOWEQ*OFF
      * Return the Check number of selected record.
     C           SELIO     IFEQ 1
     C                     Z-ADDPCHK#     CHKNBR
     C                     CLEARSELIO
     C                     UPDATSFL2
     C                     LEAVE
     C                     ENDIF
     C                     READCSFL2                     98
     C                     ENDDO
      *
      *  If variables returned, set message display indicator.
     C           CUSNBR    IFNE 0
     C           INVNBR    ANDNE0
     C           CHKNBR    ANDNE0
     C                     MOVE @TRUE     @ERR
     C                     MOVE *ON       *IN11
     C                     ENDIF
      *
     C                     ENDSR
      *
      *==============================================================*
      * F03 key subroutine                                           *
      *==============================================================*
      *
     C           @F03SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
      *
     C                     ENDSR
      *
      *==============================================================*
      * F12 key subroutine                                           *
      *==============================================================*
      *
     C           @F12SR    BEGSR
      *
      * set exit program flag
     C                     MOVE @TRUE     @EXIT
      *
     C                     ENDSR
